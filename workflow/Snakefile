import os
import sys
import math
import pandas as pd
from snakemake.utils import min_version

min_version("6.4.0")
shell.prefix("set -eo pipefail; ")
SDIR = os.path.realpath(os.path.dirname(srcdir("Snakefile")))


#
# Inputs
#
assert "vcf" in config, "vcf file must be specified in config!"
VCF = config["vcf"]

#
# Other inputs
#
CHAIN = config.get("chain", "results/chain/out-to-ref.chain")

ANCESTOR_FA = config.get("ancestor", "results/ancestor.fa")
ANCESTOR_FAI = f"{ANCESTOR_FA}.fai"

REF = config.get("reference", "")
FAI = f"{REF}.fai"

OUTGROUP = config.get("outgroup", "")

#
# Check inputs
#
if "ancestor" not in config:
    assert (
        "reference" in config and "outgroup" in config
    ), "Without an ancestor file a reference file and an outgroup file must be specified!"

    assert os.path.exists(FAI), "reference fasta index file not found!"
    CHRS = [line.split()[0] for line in open(FAI).readlines()]
else:
    assert os.path.exists(
        ANCESTOR_FAI
    ), f"ancestor fasta index file not found! {ANCESTOR_FAI}"
    CHRS = [line.split()[0] for line in open(ANCESTOR_FAI).readlines()]

if "stratify" in config:
    RGNS = config["stratify"].keys()
else:
    RGNS = []

# remove unwanted contigs
unwanted_contigs = {"chrY", "chrM", "chrMt", "chrMT", "chrEbv", "chrEBV"}
CHRS = [contig for contig in CHRS if contig not in unwanted_contigs]


#
#
#
wildcard_constraints:
    chrm="|".join(CHRS),
    rng="|".join(RGNS),


#
# rules
#
include: "rules/mutyper.smk"
include: "rules/plot.smk"


rule all:
    input:
        rules.mutyper_vcf.output,
        rules.mutyper_spectra.output,
        rules.plot_spectra.output,
        expand(rules.mutyper_spectra_targets.output, rgn=RGNS),
        expand(rules.mutyper_spectra_stratify.output, rgn=RGNS),
        expand(rules.plot_spectra_stratify.output, rgn=RGNS),
        make_plot_comparison_outputs,
        expand(rules.mutyper_spectra_correction.output, rgn=RGNS),
        expand(rules.mutyper_spectra_ksfs.output, rgn=RGNS),
        rules.seq_content.output,
